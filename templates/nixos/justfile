alias bo := boot
alias bu := build
alias sw := switch
alias t := test

rebuild := if os() == "macos" { "darwin-rebuild" } else { "nixos-rebuild" }

default:
	@just --choose

# Wrapper for `nixos-rebuild`. See `man nixos-rebuild`
rebuild subcmd *args="":
    {{ rebuild }} {{ subcmd }} --flake . {{ args }}

# Switch to your new configuration when you reboot
boot *args="": (rebuild "boot" args)

# Build your new configuration
build *args="": (rebuild "build" args)

# Immediately switch to your new configuration
switch *args="": (rebuild "switch" args)

# Temporarily switch to your new configuration
test *args="": (rebuild "test" args)
